# CMake package configuration file for the @PROJECT_NAME@ module

@PACKAGE_GUARD@
@PACKAGE_INIT@

set(SOFA_QT_HAVE_QT5 @SOFA_QT_HAVE_QT5@)
set(SOFA_QT_HAVE_QT6 @SOFA_QT_HAVE_QT6@)

set(SOFA_QT_ENABLE_QTVIEWER @SOFA_QT_ENABLE_QTVIEWER@)
set(SOFA_QT_ENABLE_QGLVIEWER @SOFA_QT_ENABLE_QGLVIEWER@)
set(SOFA_QT_ENABLE_NATIVE_MENU @SOFA_QT_ENABLE_NATIVE_MENU@)
set(SOFA_QT_ENABLE_VSYNC @SOFA_QT_ENABLE_VSYNC@)

set(SOFA_QT_HAVE_QT5_CHARTS @SOFA_QT_HAVE_QT5_CHARTS@)
set(SOFA_QT_HAVE_QT5_WEBENGINE @SOFA_QT_HAVE_QT5_WEBENGINE@)
set(SOFA_QT_HAVE_NODEEDITOR @SOFA_QT_HAVE_NODEEDITOR@)
set(SOFA_QT_HAVE_TINYXML @SOFA_QT_HAVE_TINYXML@)

set(SOFA_QT_HAVE_QT6_CHARTS @SOFA_QT_HAVE_QT6_CHARTS@)
set(SOFA_QT_HAVE_QT6_WEBENGINECORE @SOFA_QT_HAVE_QT6_WEBENGINECORE@)

# This is needed to be able to find Sofa.Qt/lib/cmake/QGLViewer
list(APPEND CMAKE_PREFIX_PATH "${CMAKE_CURRENT_LIST_DIR}/..")

find_package(Sofa.GUI.Common QUIET REQUIRED)
find_package(Sofa.Component.Visual QUIET REQUIRED)
find_package(Sofa.Component.SceneUtility QUIET REQUIRED)

if(SOFA_QT_HAVE_QT5)
    find_package(Qt5 QUIET REQUIRED Core Gui OpenGL)
    if(SOFA_QT_HAVE_QT5_CHARTS)
        find_package(Qt5 QUIET REQUIRED Charts)
    endif()
    if(SOFA_QT_HAVE_QT5_WEBENGINE)
        find_package(Qt5 QUIET REQUIRED WebEngine WebEngineWidgets)
    endif()
    if(SOFA_QT_ENABLE_QGLVIEWER)
        find_package(QGLViewer QUIET REQUIRED)
    endif()
elseif(SOFA_QT_HAVE_QT6)
    find_package(Qt6 COMPONENTS Widgets OpenGLWidgets QUIET REQUIRED)
    if(SOFA_QT_HAVE_QT6_CHARTS)
        find_package(Qt6 QUIET REQUIRED Charts)
    endif()
    if(SOFA_QT_HAVE_QT6_WEBENGINECORE)
        find_package(Qt6 QUIET REQUIRED Positioning WebChannel WebEngineCore WebEngineWidgets)
    endif()
    if(SOFA_QT_ENABLE_QGLVIEWER)
        find_package(QGLViewer QUIET REQUIRED)
    endif()
endif()

if(SOFA_QT_HAVE_NODEEDITOR)
    find_package(NodeEditor QUIET REQUIRED)
endif()


if(NOT TARGET @PROJECT_NAME@)
    include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake")
endif()

check_required_components(@PROJECT_NAME@)
